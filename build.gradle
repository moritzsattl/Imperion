plugins {
    id 'java'
    // add a shadow plugin to create a fat jar that includes all dependencies
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group 'at.ac.tuwien.ifs.sge'
version '1.0.0'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    if (project.hasProperty("localDevelopment") && localDevelopment) {
        implementation project(':rt-sge-core')
        implementation project(':rt-sge-empire')
    } else {
        implementation 'com.gitlab.rt-sge:core:1.1.0'
        implementation 'com.gitlab.rt-sge:rt-sge-empire:1.1.3'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

jar {
// specify manifest attributes
    manifest {
        attributes 'Sge-Type': 'agent'
        attributes 'Agent-Name': 'Imperion'
        attributes 'Main-Class': 'at.ac.tuwien.ifs.sge.agent.Imperion'
        attributes 'Sge-Realtime': true
    }
}
// task to create the fat jar which can be started by the engine
shadowJar {
    archiveClassifier.set('exe');
}
